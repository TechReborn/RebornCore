buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "Modmuss50"
			url = "http://maven.modmuss50.me/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8


repositories {
	maven {
		name = "Modmuss50"
		url = "http://maven.modmuss50.me/"
	}
}


configurations {
	shade
	compile.extendsFrom shade
}

version = "3.0.0-SNAPSHOT"


def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
	version = version + "." + "${System.getenv().BUILD_NUMBER}"
}

minecraft {
	version = "1.11.2-13.20.0.2201"
	mappings = "snapshot_20161118"
	replace "@MODVERSION@", project.version
	runDir = "run"
	makeObfSourceJar = false
}

group = 'RebornCore'


dependencies {

}

processResources
	{
		from(sourceSets.main.resources.srcDirs) {
			include 'mcmod.info'
			expand 'version': project.version, 'mcversion': project.minecraft.version
		}

		from(sourceSets.main.resources.srcDirs) {
			exclude 'mcmod.info'
		}
	}


task deobfJar(type: Jar) {
	from sourceSets.main.output
	exclude "**/*.psd"
	classifier = 'dev'
	manifest {
		attributes 'FMLCorePlugin': 'teamreborn.reborncore.RebornCoreLoadingPlugin'
		attributes 'FMLCorePluginContainsFMLMod': 'true'
	}
}

jar {
	exclude "**/*.psd"
	classifier = 'universal'
	manifest {
		attributes 'FMLCorePlugin': 'teamreborn.reborncore.RebornCoreLoadingPlugin'
		attributes 'FMLCorePluginContainsFMLMod': 'true'
	}
}

task apiJar(type: Jar) {
	from sourceSets.main.allSource
	from sourceSets.main.output
	include 'reborncore/api/**/*'

	classifier = "api"
}

apiJar.mustRunAfter deobfJar

artifacts {
	archives deobfJar
	archives apiJar
}

uploadArchives {
	repositories {
		mavenDeployer {
			//This is used when the build is done on a slave node
			if (project.hasProperty('mavenPass')) {
				println 'Uploading to remote Maven Repo'
				repository(url: "http://mavenupload.modmuss50.me/") {
					authentication(userName: "buildslave", password: project.getProperty('mavenPass'))
				}
			} else {
				//this is when the build is executed on the master node
				println 'Uploading to local Maven Repo'
				repository(url: "file:///var/www/maven/")
			}

			pom.whenConfigured {
				p ->
					p.dependencies = p.dependencies.findAll {
						dep -> dep.artifactId != "Tesla"
					}
			}

			pom {
				groupId = "RebornCore"
				version = project.version
				artifactId = project.archivesBaseName
				project {
					name project.archivesBaseName
					packaging 'jar'
					description 'RebornCore'
					url 'https://github.com/TechReborn/RebornCore'
					scm {
						url 'https://github.com/TechReborn/RebornCore'
						connection 'scm:git:git@github.com:TechReborn/RebornCore.git'
						developerConnection 'scm:git:git@github.com:TechReborn/RebornCore.git'
					}
					issueManagement {
						system 'github'
						url 'https://github.com/TechReborn/RebornCore/issues'
					}
					licenses {
						license {
							name 'License'
							url 'https://raw.githubusercontent.com/TechReborn/RebornCore/master/LICENSE.md'
							distribution 'repo'
						}
					}
					developers {
						developer {
							id 'modmuss50'
							name 'modmuss50'
							roles {role 'developer'}
						}
						developer {
							id 'Gigabit101'
							name 'Gigabit101'
							roles {role 'developer'}
						}
					}
				}
			}
		}
	}
}

task wrapper(type: Wrapper) {
	//To update the wrapper change the version bellow then run: gradle wrapper
	gradleVersion = '3.4'
}

//Thanks Matthew from #ForgeGradle https://gist.github.com/matthewprenger/108265e3efc83181bd2cc2d2fc31a853
task updateMappings(dependsOn: sourceJar) {

	def remapped = file('src_remapped/main/java')
	def methods = file('methods.csv')
	def fields = file('fields.csv')

	doLast {
		Map<String, String> map = new HashMap<>()
		methods.eachLine {line ->
			String[] pts = line.split(",")
			map.put(pts[0], pts[1])
		}
		fields.eachLine {line ->
			String[] pts = line.split(",")
			map.put(pts[0], pts[1])
		}

		copy {
			from zipTree(sourceJar.archivePath)
			into remapped
			include '**/*.java'

			filter {javaLine ->
				map.entrySet().each {entry ->
					javaLine = javaLine.replace(entry.getKey(), entry.getValue())
				}
				return javaLine
			}

			includeEmptyDirs = false
			eachFile {println "Processing: $it.name"}
		}
	}
}

apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/ideconfig.gradle'

import groovy.util.XmlSlurper
import org.apache.commons.io.FileUtils

task curseTools << {
	def cacheFiles = new File(".gradle/curseTools")
	if (!cacheFiles.exists()) {
		cacheFiles.mkdirs()
	}

	def mavenMetadata = new File(cacheFiles, "maven-metadata.xml")

	logger.lifecycle(":downloading curseTools metadata")
	FileUtils.copyURLToFile(new URL("http://maven.modmuss50.me/CurseTools/CurseTools/maven-metadata.xml"), mavenMetadata)

	def metadata = new XmlSlurper().parseText(FileUtils.readFileToString(mavenMetadata))
	def curseToolsVersion = metadata.versioning.release

	def curseTools = new File(cacheFiles, "${curseToolsVersion}.jar")

	logger.lifecycle(":downloading CurseTools")
	FileUtils.copyURLToFile(new URL("http://maven.modmuss50.me/CurseTools/CurseTools/${curseToolsVersion}/CurseTools-${curseToolsVersion}.jar"), curseTools)

	logger.lifecycle(":launching CurseTools")
	ant.java(jar: curseTools.getAbsolutePath(), fork: true, spawn: false) {
		arg(value: jar.archivePath.getAbsolutePath())
		arg(value: deobfJar.archivePath.getAbsolutePath())
		arg(value: apiJar.archivePath.getAbsolutePath())
	}
}
