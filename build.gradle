buildscript {
	repositories {
		jcenter()
		mavenCentral()
        maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "net.fabricmc:fabric-loom:0.4-SNAPSHOT"
		classpath "gradle.plugin.net.minecrell:licenser:0.2.1"
	}
}

apply plugin: net.fabricmc.loom.LoomGradlePlugin
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java-library'
apply plugin: net.minecrell.gradle.licenser.Licenser

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	jcenter()
}

version = "4.2.10"
group = 'RebornCore'

def ENV = System.getenv()
def build_number = ENV.BUILD_NUMBER ?: "local"
version = "${version}+build.$build_number"

license {
	header file('HEADER')
	include '**/*.java'
	ignoreFailures = true //Stops the build from failing if a file does not have a license header
}

configurations {
	ctRuntime {}
}

dependencies {
	minecraft "com.mojang:minecraft:20w22a"
	mappings "net.fabricmc:yarn:20w22a+build.6:v2"
	modImplementation "net.fabricmc:fabric-loader:0.8.5+build.199"

	//Fabric api
	modImplementation "net.fabricmc.fabric-api:fabric-api:0.11.2+build.351-1.16"

	compileOnly "com.google.code.findbugs:jsr305:3.0.2"

	modApi 'teamreborn:energy:0.1.0'
	include 'teamreborn:energy:0.1.0'

	ctRuntime "CurseTools:CurseTools:+"
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}


task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	repositories {
		maven {
			url "http://mavenupload.modmuss50.me/"
			if (project.hasProperty('mavenPass')) {
				credentials {
					username 'buildslave'
					password project.getProperty('mavenPass')
				}
			}
		}
	}
}

task curseTools {
	doLast {
		logger.lifecycle(":launching CurseTools")
		ant.setLifecycleLogLevel "INFO"
		ant.java(classname: 'me.modmuss50.cursetools.Main', classpath: configurations.ctRuntime.asPath, fork: true, spawn: false) {
			arg(value: remapJar.archivePath.getAbsolutePath())
		}
	}
}

task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.runtime
}
